plugins {
    id 'java'
    id 'maven-publish'
    id 'signing'
    id 'org.beryx.jar' version '2.0.0'
}

group 'io.vproxy'
version '1.0.1-jdk9'

sourceCompatibility = '9'
targetCompatibility = '9'

tasks.withType(JavaCompile) {
    options.encoding = 'UTF-8'
}

repositories {
    mavenCentral()
}

dependencies {
    testImplementation group: 'junit', name: 'junit', version: '4.13.2'
}

java {
    withSourcesJar()
    withJavadocJar()
}

javadoc {
    exclude '**/*.java'
}

tasks.withType(GenerateModuleMetadata) {
  enabled = false
}

publishing {
    publications {
        maven(MavenPublication) {
            from components.java
            artifactId 'kotlin-stdlib-lite'

            pom {
                name = 'kotlin-stdlib-lite'
                description = 'lite version of kotlin stdlib for certain projects'
                url = 'http://github.com/vproxy-tools/kotlin-stdlib-lite'
                licenses {
                    license {
                        name = 'MIT License'
                        url = 'https://github.com/vproxy-tools/kotlin-stdlib-lite/blob/master/LICENSE'
                    }
                }
                developers {
                    developer {
                        id = 'wkgcass'
                        name = 'K.G. Wang'
                        email = 'wkgcass@hotmail.com'
                    }
                }
                scm {
                    connection = 'scm:git:git://github.com/vproxy-tools/kotlin-stdlib-lite.git'
                    developerConnection = 'scm:git:git://github.com/vproxy-tools/kotlin-stdlib-lite.git'
                    url = 'https://github.com/vproxy-tools/kotlin-stdlib-lite'
                }
            }
        }
    }
    repositories {
        maven {
            credentials {
                username 'wkgcass'
                password System.getProperty("MavenPublishPassword")
            }
            def releasesRepoUrl = 'https://s01.oss.sonatype.org/service/local/staging/deploy/maven2/'
            def snapshotsRepoUrl = 'https://s01.oss.sonatype.org/content/repositories/snapshots/'
            url = version.contains('-DEV') ? snapshotsRepoUrl : releasesRepoUrl
        }
    }
}

signing {
    sign publishing.publications.maven
}

